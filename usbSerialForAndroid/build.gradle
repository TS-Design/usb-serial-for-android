plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 33
        consumerProguardFiles 'proguard-rules.pro'
        
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments = [                    // Raspi   Windows   LinuxVM   ...
                'rfc2217_server_host': '192.168.0.186',
                'rfc2217_server_nonstandard_baudrates': 'true',   // true    false     false
        ]
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    configurations.implementation {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
    }

}

dependencies {
    implementation 'androidx.annotation:annotation:1.6.0'
    implementation 'androidx.test:core:1.5.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.2.0'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'commons-net:commons-net:20030805.205232'
    androidTestImplementation 'org.apache.commons:commons-lang3:3.12.0'
}

// gradle task: publishToMavenLocal
project.afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release // change to anyDeviceRelease if coverage is enabled
                artifact androidSourcesJar

                // values used for local maven repo, jitpack uses github release:
                groupId 'com.github.mik3y'
                artifactId 'usb-serial-for-android'
                version 'v3.5.0beta'
            }
        }
    }
}

task androidSourcesJar(type: Jar) {
    classifier 'sources'
    from android.sourceSets.main.java.srcDirs
}

//apply from: 'coverage.gradle'
